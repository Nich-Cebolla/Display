
#include ..\struct
#include Display_Point.ahk
#include Display_Rect.ahk
#include Display_TcItemW.ahk

/**
 * @description - Calculates a tab control's display area given a window rectangle, or calculates
 * the window rectangle that would correspond to a specified display area.
 *
 * The display area is the area within which the tab's controls are visible. The window area is the
 * window's entire area, including tabs and margins.
 *
 * {@link https://learn.microsoft.com/en-us/windows/win32/controls/tcm-adjustrect}.
 *
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Operation - One of the following:
 * - 1 : `RectObj` represents the display area of the tab control; it will be modified to represent
 *   the corresponding window area.
 * - 0 : `RectObj` represents the window area of the tab control; it will be modified to represent
 *   the corresponding display area.
 *
 * @param {Display_Rect} RectObj - The {@link Display_Rect} object.
 */
TabAdjustWindowRect(TabCtrl, Operation, RectObj) {
    return SendMessage(4904, Operation, RectObj, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
}

/**
 * @description - Delets all tabs.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 */
TabDeleteAll(TabCtrl) {
    return SendMessage(4873, 0, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_DELETEALLITEMS
}

/**
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Boolean} [Scope = false] - Flag that specifies the scope of the item deselection. If TabCtrl
 * parameter is set to FALSE, all tab items will be reset. If it is set to TRUE, then all tab
 * items except for the one currently selected will be reset.
 */
TabDeselectAll(TabCtrl, Scope := false) {
    SendMessage(4914, Scope, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_DESELECTALL
}

/**
 * @description - The input is a display rectangle, and the output is the window rectangle
 * necessary for a tab control to have a display rectangle of the input dimensions.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Display_Rect} rc - The display rectangle.
 * @returns {Display_Rect} - The window rectangle (same object with new values)
 */
TabDisplayToWindow(TabCtrl, rc) {
    SendMessage(4904, true, rc, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
    return rc
}

/**
 * @description - Returns the control's display rectangle relative to the parent window.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Display_Rect}
 */
TabGetClientDisplayRect(TabCtrl) {
    rc := Display_Rect()
    if !DllCall('GetWindowRect', 'ptr', TabCtrl.Hwnd, 'ptr', rc, 'int') {
        throw OSError()
    }
    rc.ToClient(TabCtrl.Gui.Hwnd, true)
    SendMessage(4904, false, rc, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
    return rc
}

/**
 * @description - Returns the control's window rectangle relative to the parent window.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Display_Rect}
 */
TabGetClientWindowRect(TabCtrl) {
    rc := Display_Rect()
    if !DllCall('GetWindowRect', 'ptr', TabCtrl.Hwnd, 'ptr', rc, 'int') {
        throw OSError()
    }
    rc.ToClient(TabCtrl.Gui.Hwnd, true)
    return rc
}

/**
 * @description - Returns the index of the item that has the focus in a tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Integer}
 */
TabGetCurFocus(TabCtrl) {
    return SendMessage(4911, 0, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_GETCURFOCUS
}

/**
 * @description - Determines the currently selected tab in a tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Integer} - Returns the index of the selected tab if successful, or -1 if no tab is
 * selected.
 */
TabGetCurSel(TabCtrl) {
    return SendMessage(4875, 0, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_GETCURSEL
}

/**
 * @description - Calculates the top-left corner of a tab control's display area relative to the
 * gui window.
 *
 * The display area is the area within which the tab's controls are visible.
 *
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {VarRef} [OutTabRect] - A variable that will receive the {@link Display_Rect} object representing
 * the tab's display area that is generated by the function.
 *
 * @returns {Display_Point}
 */
TabGetDisplayTopLeft(TabCtrl, &OutTabRect?) {
    TabCtrl.GetPos(&tabx, &taby, &tabw, &tabh)
    OutTabRect := Display_Rect(tabx, taby, tabw + tabx, taby + tabh)
    SendMessage(4904, false, OutTabRect, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
    return Display_Point(tabx + OutTabRect.l, taby + OutTabRect.t)
}

/**
 * @description - Retrieves the extended styles that are currently in use for the tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Integer} - Returns a DWORD value that represents the extended styles currently in
 * use for the tab control. TabCtrl value is a combination of tab control extended styles.
 */
TabGetExtendedStyle(TabCtrl) {
    return SendMessage(4917, 0, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_GETEXTENDEDSTYLE
}

/**
 * @description - Retrieves the image list associated with a tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Integer} - Returns the handle to the image list if successful, or NULL otherwise.
 */
TabGetImageList(TabCtrl) {
    return SendMessage(4866, 0, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_GETIMAGELIST
}

/**
 * @description - Retrieves the number of tabs in the tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Integer} - Returns the number of items if successful, or zero otherwise.
 */
TabGetItemCount(TabCtrl) {
    return SendMessage(4868, 0, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_GETITEMCOUNT
}

/**
 * @description - Retrieves the bounding rectangle for a tab in a tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Display_Rect}
 * @throws {OSError} - If the `SendMessage` call fails.
 */
TabGetItemRect(TabCtrl, Index) {
    rc := Display_Rect()
    if !SendMessage(4874, Index, rc, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) { ; TCM_GETITEMRECT
        throw OSError('Failed to get item rect.')
    }
    return rc
}

/**
 * @description - Retrieves the current number of rows of tabs in a tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Integer} - The number of rows of tabs.
 */
TabGetRowCount(TabCtrl) {
    return SendMessage(4908, 0, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_GETROWCOUNT
}

/**
 * @description - Returns the control's display rectangle relative to the screen.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Display_Rect}
 */
TabGetScreenDisplayRect(TabCtrl) {
    rc := Display_Rect()
    if !DllCall('GetWindowRect', 'ptr', TabCtrl.Hwnd, 'ptr', rc, 'int') {
        throw OSError()
    }
    SendMessage(4904, false, rc, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
    return rc
}

/**
 * @description - Returns the control's window rectangle relative to the screen.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @returns {Display_Rect}
 */
TabGetScreenWindowRect(TabCtrl) {
    rc := Display_Rect()
    if !DllCall('GetWindowRect', 'ptr', TabCtrl.Hwnd, 'ptr', rc, 'int') {
        throw OSError()
    }
    return rc
}

/**
 * @description - Gets tab text.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Index - The index of the tab for which to get the text.
 * @param {Integer} MaxChars - The maximum characters to copy to the buffer. TabCtrl can be an
 * overestimate.
 * @returns {String} - The tab's text, or an empty string if the operation failed.
 */
TabGetTabText(TabCtrl, Index, MaxChars) {
    tcitem := Display_TcItemW(1, , , , MaxChars) ; TCIF_TEXT
    if SendMessage(4924, Index, tcitem.Ptr, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) { ; TCM_GETITEMW
        return tcitem.pszText
    } else {
        return ''
    }
}

/**
 * @description - Highlights a tab.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Index - The index of the tab to highlight / remove higlighting.
 * @param {Boolean} [Value = true] - If true, activates the highlight state. If false, deactivates
 * the highlight state.
 * @returns {Integer} - 1 if successful, 0 if unsuccessful.
 */
TabHighlightItem(TabCtrl, Index, Value := true) {
    return SendMessage(4915, Index, (0 & 0xFFFF) << 16 | (Value & 0xFFFF), TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_HIGHLIGHTITEM
}

/**
 * @description - Determines if a tab is at the input coordinate.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} X - The x-coordinate.
 * @param {Integer} Y - The y-coordinate.
 * @returns {Integer} - One of the following values:
 * - 1: The position is not over a tab.
 * - 2: The position is over a tab's icon.
 * - 4: The position is over a tab's text.
 * - 6: The position is over a tab but not over its icon or its text. For owner-drawn tab
 * controls, TabCtrl value is specified if the position is anywhere over a tab.
 */
TabHitTest(TabCtrl, X, Y) {
    HitTest := Buffer(12)
    NumPut('int', X, 'int', Y, HitTest, 0)
    SendMessage(4877, 0, HitTest.ptr, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_HITTEST
    return NumGet(HitTest, 8, 'uint')
}

/**
 * @description - Adjusts the size and position of the control to produce a display area with
 * the input dimensions. If a value is not provided, the current value is used.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} [X] - The x-coordinate.
 * @param {Integer} [Y] - The y-coordinate.
 * @param {Integer} [W] - The width.
 * @param {Integer} [H] - The height.
 * @returns {Display_Rect} - The window rectangle.
 */
TabMoveEx(TabCtrl, X?, Y?, W?, H?) {
    rc := Display_Rect()
    if !DllCall('GetWindowRect', 'ptr', TabCtrl.Hwnd, 'ptr', rc, 'int') {
        throw OSError()
    }
    rc.ToClient(TabCtrl.Gui.Hwnd, true)
    SendMessage(4904, false, rc, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
    rc := Display_Rect(X ?? rc.X, Y ?? rc.Y, (W ?? rc.W) + (X ?? rc.X), (H ?? rc.H) + (Y ?? rc.Y))
    SendMessage(4904, true, rc, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
    TabCtrl.Move(rc.X, rc.Y, rc.W, rc.H)
    return rc
}

/**
 * @description - Removes an image from a tab control's image list.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Index - The index of the image to remove.
 */
TabRemoveImage(TabCtrl, Index) {
    SendMessage(4906, Index, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_REMOVEIMAGE
}

/**
 * @description - Sets the focus to a specified tab in a tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Index - The index of the tab to focus.
 */
TabSetCurFocus(TabCtrl, Index) {
    SendMessage(4912, Index, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_SETCURFOCUS
}

/**
 * @description - Selects a tab in a tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Index - The index of the tab to select.
 */
TabSetCurSel(TabCtrl, Index) {
    SendMessage(4876, Index, 0, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_SETCURSEL
}

/**
 * @description - Sets an extended style on the tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Style - One of the following:
 * - 1: TCS_EX_FLATSEPARATORS
 * - 2: TCS_EX_REGISTERDROP
 * @param {Integer} Value - Either 1 or 0 to enable or clear the style, respectively.
 */
TabSetExtendedStyle(TabCtrl, Style, Value) {
    return SendMessage(4916, Style, Value, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_SETEXTENDEDSTYLE
}

/**
 * @description - Assigns an image list to a tab control.
 * {@link https://learn.microsoft.com/en-us/windows/win32/controls/tcm-setimagelist}
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Index - The index of the image to remove.
 * @returns {Integer} - Returns the handle to the previous image list, or NULL if there is no
 * previous image list.
 */
TabSetImageList(TabCtrl, Handle) {
    SendMessage(4867, 0, Handle, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_SETIMAGELIST
}

/**
 * @description - Sets the width and height of tabs in a fixed-width or owner-drawn tab control.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Width - The width in pixels.
 * @param {Integer} Height - The height in pixels.
 * @param {VarRef} [OutOldWidth] - A variable that will receive the previous width in pixels
 * @param {VarRef} [OutOldHeight] - A variable that will receive the previous height in pixels
 */
TabSetItemSize(TabCtrl, Width, Height, &OutOldWidth?, &OutOldHeight?) {
    old := SendMessage(4905, 0, (Height & 0xFFFF) << 16 | (Width & 0xFFFF), TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_SETITEMSIZE
    OutOldWidth := old & 0xFFFF
    OutOldHeight := (old >> 16)
}

/**
 * @description - Sets the minimum tab width.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Width - The minimum tab width in pixels.
 * @returns {Integer} - The previous minimum tab width.
 */
TabSetMinTabWidth(TabCtrl, Width) {
    return SendMessage(4913, 0, Width, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_SETMINTABWIDTH
}

/**
 * @description - Sets a tab's text.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Integer} Index - The index of the tab which will have its text changed.
 * @param {String} NewText - The new text.
 * @returns {Integer} - 1 if successful, 0 otherwise.
 */
TabSetTabText(TabCtrl, Index, NewText) {
    tcitem := Display_TcItemW(1, , , NewText) ; TCIF_TEXT
    return SendMessage(4925, Index, tcitem.Ptr, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_SETITEMW
}

/**
 * @description - The input is a window rectangle, and the output is the display rectangle
 * area for a tab control with the input dimensions.
 * @param {Gui.Tab} TabCtrl - The `Gui.Tab` control.
 * @param {Display_Rect} rc - The window rectangle. If unset, the control's current dimensions
 * are used.
 * @returns {Display_Rect} - The display rectangle (same object with new values)
 */
TabWindowToDisplay(TabCtrl, rc) {
    SendMessage(4904, false, rc, TabCtrl.Hwnd, TabCtrl.Gui.Hwnd) ; TCM_ADJUSTRECT
    return rc
}
